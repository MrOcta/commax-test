Import('arch', 'env', 'common', 'msgq')

cereal_dir = Dir('.')
gen_dir = Dir('gen')

# Build cereal
schema_files = ['log.capnp', 'car.capnp', 'legacy.capnp', 'custom.capnp']
env.Command([f'gen/cpp/{s}.c++' for s in schema_files] + [f'gen/cpp/{s}.h' for s in schema_files],
            schema_files,
            f"capnpc --src-prefix={cereal_dir.path} $SOURCES -o c++:{gen_dir.path}/cpp/")

cereal = env.Library('cereal', [f'gen/cpp/{s}.c++' for s in schema_files])

# Build messaging
services_h = env.Command(['services.h'], ['services.py'], 'python3 ' + cereal_dir.path + '/services.py > $TARGET')
bridge_lib = env.Library('bridge', ["messaging/msgq_to_zmq.cc"])
env.Program('messaging/bridge', ['messaging/bridge.cc'], LIBS=[bridge_lib, msgq, common, 'pthread'])

socketmaster = env.Library('socketmaster', ['messaging/socketmaster.cc'])

Export('cereal', 'socketmaster')

if arch != 'Darwin' and GetOption('extras'):
  env.Program('messaging/tests/test_bridge', ['messaging/tests/test_bridge.cc'], LIBS=[bridge_lib, msgq, common, 'pthread'])
